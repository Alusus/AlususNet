@merge module AlususNet {
    class AlususNetPublisher {
        def GET_PRODUCTS: "/api/products";
        def CREATE_API_TOKEN_PATH: "/api/user/api-token";
        def GET_PROJECT_STATUS_PATH: "/api/project/setup-status";
        def SETUP_PROJECT_PATH: "/api/project/setup";
        def UPLOAD_PROJECT_PATH: "/api/project/upload";
        def UPLOAD_BACKUP_PATH: "/api/project/backup/upload";
        def CREATE_BACKUP_PATH: "/api/project/backup";
        def CREATE_AUTH_TOKEN_MFA: "/api/user/verify-mfa";
        def CREATE_AUTH_TOKEN: "/api/login";
        def BATCH_SIZE: 102400;
        // 100 KB
        def HOST: "http://localhost:8400";
        @shared def host: String(CharsPtr().{
            this = System.getEnv("NASHIR_HOST");
            if this == 0 this = HOST;
        });

        def publishProjectName: String;
        def settingsFilePath: String;
        def products: Json;
        def apiKey: String;
        def authKey: String;
        def trustKey: String;
        def userName: String;
        def trustedTokenPath: String;
        def invalidCodeCounter: Int = 3;
        def verbose: bool = true;
        def termOfUseAccepted: Bool = false;
        def myWorkDir: String;

        handler this~init(projName: String, apiKey: String) {
            this.publishProjectName = projName;
            def fp: ptr[Fs.File] = Fs.openProcess("pwd", "r");
            def path: array[char, 1035];
            Fs.readLine(path~ptr, 1035, fp);
            Fs.closeProcess(fp);
            this.apiKey = apiKey;
            this.myWorkDir = String(path~ptr);
            // Remove the \n from the end of the string.
            this.myWorkDir = this.myWorkDir.slice(0, this.myWorkDir.getLength() - 1);
        }

        handler this_type(): SrdRef[AlususNetPublisher] {
            return SrdRef[AlususNetPublisher]().{ alloc()~init(String(""), String("")) };
        }

        handler this_type(projName: String): SrdRef[AlususNetPublisher] {
            return SrdRef[AlususNetPublisher]().{ alloc()~init(projName, String("")) };
        }

        handler this_type(projName: String, apiKey: String): SrdRef[AlususNetPublisher] {
            return SrdRef[AlususNetPublisher]().{ alloc()~init(projName, apiKey) };
        }

        handler this.projectName: String {
            return this.publishProjectName
        }

        handler this.acceptTermOFUse() {
            if getUserYesNoAnswer(i18nOut(
                "Before proceeding, please read and agree to the following:\n\n"
                    "Terms of Service: https://alusus.net/terms-of-service\n"
                    "Privacy Policy: https://alusus.net/privacy-policy\n"
                    "By using Alusus Net, you agree to the above terms of service and privacy policy.\n\n"
                    "Do you accept these terms? [Y/n]"
            )) == "yes" {
                this.termOfUseAccepted = true;
            } else {
                this.termOfUseAccepted = false;
                System.exit(1);
            }
        }

        handler this.publish(serverPort: int) {
            this.settingsFilePath = this.myWorkDir + String("/.deploy.settings");
            this.trustedTokenPath = this.myWorkDir + String("/.alnashir.token");
            if checkProjectName(this.projectName) {
                this.fetchProducts();
                this.fetchApiToken();
                this.setupProject();
                this.uploadProject(serverPort);
            } else {
                Console.print(i18nOut(
                    "Invalid project name. It should only contain lowercase letters, numbers, "
                        "hyphens (-), and must not start or end with a hyphen.\n"
                ));
            }
        }

        handler this.restoreDbBackup(restore_db_path: String) {
            this.trustedTokenPath = String("/.alnashir.token");
            this.fetchApiToken();
            this.uploadBackup(restore_db_path);
        }

        handler this.downloadDbBackup(backup_db_path: String) {
            this.trustedTokenPath =String("/.alnashir.token");
            this.fetchApiToken();
            this.downloadBackup(backup_db_path);
        }

        macro handle401Error {
            Console.print(i18nOut("\nExisting auth key is no longer valid. Requesting a new auth key.\n"));
            Fs.remove(this.settingsFilePath);
            this.apiKey = "";
            this.fetchApiToken();
        }

        handler this.fetchProducts() {
            while 1 {
                def request: Net.Request(String.format("%s%s", host.buf, GET_PRODUCTS));
                request.verbose = this.verbose;
                request.get();
                if request.responseHttpStatus == 200 {
                    this.products = request.responseBody;
                    if this.products.getLength() < 1 {
                        System.fail(1, i18nOut("Failed to fetch product info from AlususNet\n"));
                    }
                    break;
                } else {
                    handleUnexpectedCommunicationError[];
                }
            }
        }

        handler this.findProduct(productId: String): Json {
            def i: Int;
            for i = 0, i < this.products.getLength(), ++i {
                def product: Json = this.products(i);
                if product("id")~cast[String] == productId return product;
            }
            return Json();
        }

        handler this.fetchAuthToken() {
            def userName: array[Char, 100];
            def password: array[Char, 100];
            def mfaCode: array[Char, 100];
            macro promptForUserName{
                Console.print(i18nOut("Enter username: \n>"));
                Console.getString(userName~ptr, 100);
            }
            macro promptForPassword{
                Console.print(i18nOut("Enter password: \n>"));
                Console.getString(password~ptr, 100);
            }
            macro promptForMfaCode{
                Console.print(i18nOut("Enter mfaCode or enter back to logIn agin: \n>"));
                Console.getString(mfaCode~ptr, 100);
            }
            if Fs.exists(this.settingsFilePath) {
                def data: String = Fs.readFile(this.settingsFilePath);
                this.apiKey = Json(data)("apiKey");
            } else {
                if Fs.exists(this.trustedTokenPath) {
                    def data: String = Fs.readFile(this.trustedTokenPath);
                    this.userName = Json(data)("userName");
                    this.trustKey = Json(data)("trustKey");
                }
                promptForUserName[];
                promptForPassword[];
                while 1 {
                    def requestString: String;
                    if this.trustKey != ""and this.userName == userName~ptr {
                        requestString = String.format(
                            "{\"username\": \"%s\" , \"password\": \"%s\" ,\"trustToken\": \"%s\"}",
                            Json.escape(String(userName~ptr).trim()).buf,
                            Json.escape(String(password~ptr).trim()).buf,
                            Json.escape(this.trustKey.trim()).buf
                        );
                    } else {
                        requestString = String.format(
                            "{\"username\": \"%s\", \"password\": \"%s\"}",
                            Json.escape(String(userName~ptr).trim()).buf,
                            Json.escape(String(password~ptr).trim()).buf,
                        );
                    }
                    def request: Net.Request(host + CREATE_AUTH_TOKEN);
                    request.verbose = this.verbose;
                    request.post(requestString);
                    if request.responseHttpStatus == 200 {
                        this.authKey = Json(request.responseBody)("authToken");
                        break;
                    } else if request.responseHttpStatus == 202 {
                        promptForMfaCode[];
                        while 1 {
                            if i18nIn(mfaCode~ptr) == "back"break;
                            def verifyMfaCodeRequest: Net.Request(host + CREATE_AUTH_TOKEN_MFA);
                            verifyMfaCodeRequest.verbose = this.verbose;
                            verifyMfaCodeRequest.post(String.format(
                                "{\"username\": \"%s\" , \"password\": \"%s\" , \"code\": \"%s\"}",
                                Json.escape(String(userName~ptr).trim()).buf,
                                Json.escape(String(password~ptr).trim()).buf,
                                Json.escape(String(mfaCode~ptr).trim()).buf
                            ));
                            def status: int = verifyMfaCodeRequest.responseHttpStatus;
                            if status == 200 {
                                this.trustKey = Json(verifyMfaCodeRequest.responseBody)("trustToken");
                                this.userName = String(userName~ptr).trim();
                                this.authKey = Json(verifyMfaCodeRequest.responseBody)("authToken");
                                break 2;
                            } else {
                                if status == 401 {
                                    Console.print(i18nOut("Code is invalid!\n"));
                                    if this.invalidCodeCounter > 0 {
                                        this.invalidCodeCounter--;
                                        promptForMfaCode[];
                                    } else {
                                        break;
                                    }
                                } else {
                                    handleUnexpectedCommunicationError[];
                                }
                            }
                        }
                        promptForUserName[];
                        promptForPassword[];
                    } else if request.responseHttpStatus == 401 {
                        Console.print(i18nOut("Authentication error. Invalid username or password.\n"));
                        promptForUserName[];
                        promptForPassword[];
                    } else if request.responseHttpStatus == 429 {
                        Console.print(i18nOut(
                            "The account is locked due to multiple incorrect password attempts. Please try again later.\n"
                        ));
                        System.exit(1);
                    } else {
                        handleUnexpectedCommunicationError[];
                    }
                }
                def data: String = String.format(
                    "{\"trustKey\": \"%s\" , \"userName\": \"%s\"}",
                    this.trustKey.buf, this.userName.buf
                );
                Fs.createFile(this.trustedTokenPath, data, data.getLength());
            }
        }

        handler this.fetchApiToken() {
            if this.apiKey.trim() == "" {
                if Fs.exists(this.settingsFilePath) {
                    def data: String = Fs.readFile(this.settingsFilePath);
                    this.apiKey = Json(data)("apiKey");
                } else {
                    if this.authKey == ""this.fetchAuthToken();
                    while 1 {
                        def request: Net.Request(host + CREATE_API_TOKEN_PATH);
                        request.verbose = this.verbose;
                        request.authType = "Bearer";
                        request.authKey = this.authKey;
                        request.post(String.format(
                            "{\"name\": \"%s\"}",
                            Json.escape(this.projectName).buf
                        ));
                        if request.responseHttpStatus == 200 {
                            this.apiKey = Json(request.responseBody)("token");
                            def data: String = String.format("{\"apiKey\": \"%s\"}", this.apiKey.buf);
                            Fs.createFile(this.settingsFilePath, data, data.getLength());
                            break;
                        } else if request.responseHttpStatus == 401 {
                            Console.print(i18nOut("Authentication error.\n"));
                            def data: String = String.format("{}");
                            this.authKey == "";
                            Fs.createFile(this.settingsFilePath, data, data.getLength());
                            this.fetchAuthToken();
                        } else {
                            handleUnexpectedCommunicationError[];
                        }
                    }
                }
            }
        }

        handler this.setupProject() {
            def status: String;
            def productId: String;
            def promotionCode: String;
            while 1 {
                def request: Net.Request(String.format(
                    "%s%s?project=%s", host.buf, GET_PROJECT_STATUS_PATH, Net.uriEncode(this.projectName).buf
                ));
                request.verbose = this.verbose;
                request.authType = "Bearer";
                request.authKey = this.apiKey;
                request.get();
                if request.responseHttpStatus == 200 {
                    request.responseBody.split("&").{
                        status = this(0);
                        productId = this(1).split("=")(1);
                    };
                    break;
                } else if request.responseHttpStatus == 400 {
                    System.fail(1, i18nOut("Project name is not valid.\n"));
                } else if request.responseHttpStatus == 401 {
                    handle401Error[];
                } else {
                    handleUnexpectedCommunicationError[];
                }
            }
            if status == "active" {
                Console.print(i18nOut(
                    "Existing project with the same name exists. The project will be overwritten. Continue (yes/no)? "
                ));
                if getUserYesNoAnswer(String()) != "yes" {
                    System.exit(1);
                }
            } else if status == "inactive" {
                def product: Json = this.findProduct(productId);
                if product.getLength() == 0 {
                    System.fail(1, i18nOut(
                        "Existing, but inactive, project with the same name exists. "
                            "This project is in an invalid state and cannot be re-activated. "
                            "Contact AlususNet's support to resolve the issue, or change your "
                            "project's name to create a new project on AlususNet.\n"
                    ));
                }
                Console.print(i18nOut(
                    "Existing, but inactive, project with the same name exists. "
                        "The project will be re-activated and overwritten. "
                        "A new payment plan will be setup.\n"
                ));
                Console.print(i18nOut("Hosting plan:\n"));
                Console.print(getProductInfoString(product));
                while 1 {
                    def answer: String = getUserMultipleOptionsAnswer(
                        i18nOut(
                            "Continue? (Type `yes` to proceed, `no` to cancel, "
                                "or `coupon` to enter a promotion code) "
                        ),
                        Map[String, String]()
                            .set(String("yes"), String("yes"))
                            .set(String("y"), String("yes"))
                            .set(String("no"), String("no"))
                            .set(String("n"), String("no"))
                            .set(String("coupon"), String("coupon"))
                            .set(String("c"), String("coupon"))
                    );
                    if answer == "yes" {
                        break;
                    } else if answer == "no" {
                        System.exit(1);
                    } else if answer == "coupon" {
                        promotionCode = getUserTextAnswer(i18nOut("Enter promotion code: "), true);
                    }
                }
                if this.termOfUseAccepted == false this.acceptTermOFUse();
            } else {
                def product: Json = this.products(0);
                productId = product("id");
                Console.print(i18nOut(
                    "No existing project found with the same name. "
                        "A new project will be created and a payment plan will be setup for it.\n"
                ));
                Console.print(i18nOut("Hosting plan:\n"));
                Console.print(getProductInfoString(product));
                def options: Map[String, String]() = Map[String, String]()
                    .set(String("yes"), String("yes"))
                    .set(String("y"), String("yes"))
                    .set(String("no"), String("no"))
                    .set(String("n"), String("no"))
                    .set(String("coupon"), String("coupon"))
                    .set(String("c"), String("coupon"));
                def prompt: String;
                if this.products.getLength() > 1 {
                    prompt = i18nOut(
                        "Continue? (Type `yes` to proceed, `no` to cancel, "
                            "`plan` to change the hosting plan, "
                            "or `coupon` to enter a promotion code) "
                    );
                    options
                        .set(String("plan"), String("plan"))
                        .set(String("p"), String("plan"))
                } else {
                    prompt = i18nOut(
                        "Continue? (Type `yes` to proceed, `no` to cancel, "
                            "or `coupon` to enter a promotion code) "
                    );
                }
                while 1 {
                    def answer: String = getUserMultipleOptionsAnswer(prompt, options);
                    if answer == "yes" {
                        break;
                    } else if answer == "no" {
                        System.exit(1);
                    } else if answer == "plan" {
                        productId = this.selectProduct();
                    } else if answer == "coupon" {
                        promotionCode = getUserTextAnswer(i18nOut("Enter promotion code: "), true);
                    }
                }
                if this.termOfUseAccepted == false this.acceptTermOFUse();
            }
            while 1 {
                def request: Net.Request(String.format(
                    "%s%s?project=%s&product=%s&promotion_code=%s",
                    host.buf, SETUP_PROJECT_PATH,
                    Net.uriEncode(this.projectName).buf,
                    Net.uriEncode(productId).buf,
                    Net.uriEncode(promotionCode).buf
                ));
                request.verbose = this.verbose;
                request.authType = "Bearer";
                request.authKey = this.apiKey;
                request.post("");
                if request.responseHttpStatus == 200 {
                    break;
                } else if request.responseHttpStatus == 402 {
                    Console.print(
                        i18nOut(
                            "Please setup your payment profile by visiting this URL:\n"
                                "%s\nPress enter when you're done.\n"
                        ),
                        request.responseBody.buf
                    );
                    Console.getChar();
                } else if request.responseHttpStatus == 400 and request.responseBody == "promotion_code" {
                    def answer: String = getUserMultipleOptionsAnswer(
                        i18nOut(
                            "Provided promotion code is invalid. Re-enter prmotion code? "
                                "(Type `yes` to enter a new code or `cancel` to quit) "
                        ),
                        Map[String, String]()
                            .set(String("yes"), String("yes"))
                            .set(String("y"), String("yes"))
                            .set(String("cancel"), String("cancel"))
                            .set(String("c"), String("cancel"))
                    );
                    if answer == "yes" {
                        promotionCode = getUserTextAnswer(i18nOut(
                            "Enter promotion code or leave empty to proceed without a promotion code: "
                        ), true);
                    } else if answer == "cancel" {
                        System.exit(1);
                    }
                } else if request.responseHttpStatus == 401 {
                    handle401Error[];
                } else {
                    handleUnexpectedCommunicationError[];
                }
            }
        }

        handler this.uploadProject(serverPort: int) {
            def order: String = String("rm -rf \"") + this.projectName + "\"";
            System.exec(order);
            order = String("mv Build \"") + this.projectName + "\"";
            System.exec(order);
            def tarFilename: String;
            tarFilename = this.projectName + String(".tar.gz");
            order = String("tar cvzf  \"") + tarFilename + "\"  -C \"" + this.myWorkDir + "/" + this.projectName + "\" .";
            System.exec(order);
            def buffer: UnqRef[array[Char, BATCH_SIZE]];
            buffer.construct();
            while 1 {
                def fp: ptr[Fs.File] = Fs.openFile(tarFilename, "rb");
                Fs.seek(fp, 0, Fs.Seek.END);
                def totalSize: ArchInt = Fs.tell(fp);
                Fs.seek(fp, 0, Fs.Seek.SET);
                def startOffset: ArchInt = 0;
                while startOffset < totalSize {
                    Console.print(i18nOut("Uploading: %d%%"), (startOffset * 100 / totalSize)~cast[Int]);
                    Console.print("\r");
                    Fs.flush(0~cast[ptr[Fs.File]]);
                    def batchSize: ArchInt = Fs.read(buffer, 1, BATCH_SIZE, fp);
                    if batchSize == 0 {
                        Console.print(i18nOut("Unexpected error. Try re-starting the deployment.\n"));
                        System.exit(1);
                    }
                    def request: Net.Request(String.format(
                        "%s%s?project=%s&port=%i", host.buf, UPLOAD_PROJECT_PATH,
                        Net.uriEncode(this.projectName).buf, serverPort
                    ));
                    request.verbose = this.verbose;
                    request.authKey = this.apiKey;
                    request.authType = "Bearer";
                    request.contentType = "application/octet-stream";
                    request.addHeader(String.format("Content-Total-Length: %i", totalSize~cast[Int]));
                    request.addHeader(String.format("Content-Start-Offset: %i", startOffset~cast[Int]));
                    request.put(buffer, batchSize);
                    if request.responseHttpStatus == 200 {
                        startOffset += batchSize;
                    } else if request.responseHttpStatus == 400 {
                        Console.print(i18nOut("Upload failed. File is too big!\n"));
                        System.exit(1);
                    } else if request.responseHttpStatus == 404 {
                        Console.print(i18nOut("Unexpected error. Try re-starting the deployment.\n"));
                        System.exit(1);
                    } else if request.responseHttpStatus == 423 {
                        Console.print(i18nOut(
                            "Some operation is currently in progress on this project's container. Retry (yes/no)? "
                        ));
                        if getUserYesNoAnswer(String()) != "yes" {
                            System.exit(1);
                        }
                        continue 2;
                    } else if request.responseHttpStatus == 401 {
                        handle401Error[];
                        continue 2;
                    } else {
                        handleUnexpectedCommunicationError[];
                        continue 2;
                    }
                }
                Console.print(i18nOut("Uploading: %d%%"), 100);
                Console.print("\n");
                break;
            }
            Fs.remove(tarFilename);
            Console.print(i18nOut("Deployment successful.\n"));
        }

        handler this.uploadBackup(filePath: String) {
            def buffer: UnqRef[array[Char, BATCH_SIZE]];
            buffer.construct();
            while 1 {
                def fp: ptr[Fs.File] = Fs.openFile(filePath, "rb");
                Fs.seek(fp, 0, Fs.Seek.END);
                def totalSize: ArchInt = Fs.tell(fp);
                Fs.seek(fp, 0, Fs.Seek.SET);
                def startOffset: ArchInt = 0;
                while startOffset < totalSize {
                    Console.print(i18nOut("Uploading: %d%%"), (startOffset * 100 / totalSize)~cast[Int]);
                    Console.print("\r");
                    Fs.flush(0~cast[ptr[Fs.File]]);
                    def batchSize: ArchInt = Fs.read(buffer, 1, BATCH_SIZE, fp);
                    if batchSize == 0 {
                        Console.print(i18nOut("Unexpected error. Try re-uploading backup.\n"));
                        System.exit(1);
                    }
                    def request: Net.Request(String.format(
                        "%s%s?project=%s", host.buf, UPLOAD_BACKUP_PATH,
                        Net.uriEncode(this.projectName).buf
                    ));
                    request.verbose = this.verbose;
                    request.authKey = this.apiKey;
                    request.authType = "Bearer";
                    request.contentType = "application/octet-stream";
                    request.addHeader(String.format("Content-Total-Length: %i", totalSize~cast[Int]));
                    request.addHeader(String.format("Content-Start-Offset: %i", startOffset~cast[Int]));
                    request.put(buffer, batchSize);
                    if request.responseHttpStatus == 200 {
                        startOffset += batchSize;
                    } else if request.responseHttpStatus == 400 {
                        Console.print(i18nOut("Upload failed. File is too big!\n"));
                        System.exit(1);
                    } else if request.responseHttpStatus == 404 {
                        Console.print(i18nOut("Unexpected error. Try re-uploading the database.\n"));
                        System.exit(1);
                    } else if request.responseHttpStatus == 401 {
                        handle401Error[];
                        continue 2;
                    } else {
                        handleUnexpectedCommunicationError[];
                        continue 2;
                    }
                }
                Console.print(i18nOut("Uploading: %d%%"), 100);
                Console.print("\n");
                break;
            }
            Console.print(i18nOut("Database restored successful.\n"));
        }

        handler this.downloadBackup(filePath: String) {
            def downloadUrl: String = this.createBackup();
            while 1 {
                if Net.get(downloadUrl.buf, filePath.buf) == false {
                    handleUnexpectedCommunicationError[];
                } else {
                    break;
                }
            }
        }

        handler this.createBackup(): String {
            def downloadUrl: String = "http://localhost:8400//api/backup/download?name=database_sleman2-MyProject3_2025-4-30_4:0&token=da3ad7969c00e9a3940fd51f0228a508";
            if downloadUrl == "" {
                if this.apiKey == ""this.fetchApiToken();
                while 1 {
                    def request: Net.Request(String.format(
                        "%s%s?project=%s&backup_type=database", host.buf, CREATE_BACKUP_PATH,
                        Net.uriEncode(this.projectName).buf
                    ));
                    request.verbose = this.verbose;
                    request.authType = "Bearer";
                    request.authKey = this.apiKey;
                    request.contentType = "application/json";
                    request.post(String("{}"));
                    if request.responseHttpStatus == 200 {
                        downloadUrl = Json(request.responseBody)("downloadUrl");
                        return downloadUrl;
                    } else if request.responseHttpStatus == 401 {
                        handle401Error[];
                    } else if request.responseHttpStatus == 404 {
                        Console.print(i18nOut("project not found\n"));
                        break;
                    } else if request.responseHttpStatus == 423 {
                        Console.print(i18nOut(
                            "Some operation is currently in progress on this project's container. Retry (yes/no)? "
                        ));
                        if getUserYesNoAnswer(String()) != "yes" {
                            System.exit(1);
                        }
                    } else {
                        handleUnexpectedCommunicationError[];
                    }
                }
            }
            return downloadUrl;
        }


        handler this.selectProduct(): String {
            Console.print(i18nOut("Select a plan:\n"));
            def i: Int;
            def options: Map[String, String];
            for i = 0, i < this.products.getLength(), ++i {
                Console.print(
                    "%s%d%s: %s",
                    Console.Style.FG_GREEN, i + 1, Console.Style.RESET,
                    getProductInfoString(this.products(i)).buf
                );
                def num: String = String() + (i + 1);
                options.set(num, num);
            }
            def answer: String = getUserMultipleOptionsAnswer(String(), options);
            return this.products(String.parseInt(answer) - 1)("id");
        }

        func getProductInfoString(product: Json): String {
            return String.format(
                i18nOut("%s - %s.\nMonthly cost: $%s USD\n"),
                product("name")~cast[CharsPtr],
                product("description")~cast[CharsPtr],
                formatPrice(product("price")).buf
            );
        }

        func formatPrice(f: Float): String {
            def result: String;
            if f < 0 {
                f *=  - 1;
                result.append('-');
            }
            result.append(f~cast[Int[64]]);
            def fractions: Float[64] = f - f~cast[Int[64]];
            result.append('.');
            result._append((fractions * 100.0f64 + 0.5f64)~cast[Int[64]], 2);
            return result;
        }

        func checkProjectName(projectName: String): Bool {
            def matcher: Regex.Matcher("^[a-z0-9\\-]+$");
            return matcher.match(projectName.buf).getLength() > 0;
        }
    }
}

